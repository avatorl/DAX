//Simple date table
VAR _StartDate =
    --first date
    MIN ( 'Vaccinations'[Covid Vacc Date] )
VAR _EndDate =
    --last date
    MAX ( 'Vaccinations'[Covid Vacc Date] )
VAR _BaseCalendar =
    CALENDAR ( _StartDate, _EndDate )
VAR _FullCalendar =
    ADDCOLUMNS (
        _BaseCalendar,
        //20210712 (number) - date key
        "Date Key", VALUE ( FORMAT ( [Date], "yyyyMMdd" ) ),
        //calendar year =============================================
        --2021 (number)
        "Year Number", YEAR ( [Date] ),
        --2021 (string)
        "Year", FORMAT ( [Date], "yyyy" ),
        --CY21 (string)
        "Calendar Year", "CY" & FORMAT ( [Date], "yy" ),
        //calendar month ============================================
        --7 (number)
        "Month Number", MONTH ( [Date] ),
        --July
        "Month Long", FORMAT ( [Date], "mmmm" ),
        --Jul
        "Month Short", FORMAT ( [Date], "mmm" ),
        --Jul'21
        "Month Short'Year", FORMAT ( [Date], "mmm'yy" ),
        //calendar week =============================================
        --202128 (number)
        "Year Week Order",
            YEAR ( [Date] ) * 100
                + WEEKNUM ( [Date], 2 ),
        --28 (number)
        "Week Number", WEEKNUM ( [Date], 2 ),
        --W28
        "Week W", "W" & WEEKNUM ( [Date], 2 ),
        //week day ==================================================
        --1 (number)
        "Week Day Number", WEEKDAY ( [Date], 2 ),
        --Mon
        "Week Day Short", FORMAT ( [Date], "ddd" ),
        --Monday
        "Week Day Long", FORMAT ( [Date], "dddd" ),
        //other =====================================================
        --Jul 12
        "Month Short and Date", FORMAT ( [Date], "mmm dd" ),
        --12 (number)
        "Day of Month", DAY ( [Date] ),
        --193
        "Day of Year", DATEDIFF ( DATE ( YEAR ( [Date] ), 1, 1 ), [Date], DAY ) + 1,
        --43151 (number)
        "Day Since 1900", VALUE ( [Date] )
    )
RETURN
    _FullCalendar
