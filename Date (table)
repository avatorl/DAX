//Date table
// Configuration
VAR _StartDate =
    --first date
    DATE ( 2021, 1, 1 )
VAR _EndDate =
    --last date
    DATE ( 2022, 11, 31 )
VAR _BaseCalendar =
    CALENDAR ( _StartDate, _EndDate )
VAR _FYOffset = 6
VAR _ZeroLenSpace =
    UNICHAR ( 8203 ) //
// Table
VAR _FullCalendar =
    ADDCOLUMNS (
        _BaseCalendar,
        //20210712
        "Date Key", VALUE ( FORMAT ( [Date], "yyyyMMdd" ) ),
        // year =============================================
        --2021
        "Year", YEAR ( [Date] ),
        --2021
        "Fiscal Year", YEAR ( EOMONTH ( [Date], _FYOffset ) ),
        --CY21
        "Calendar Year (CY)", "CY" & FORMAT ( [Date], "yy" ),
        --CY21
        "Fiscal Year (FY)", "FY" & FORMAT ( EOMONTH ( [Date], _FYOffset ), "yy" ),
        // month ============================================ 
        --7
        "Month", MONTH ( [Date] ),
        --July
        "Month Long",
            REPT ( _ZeroLenSpace, 12 - MONTH ( [Date] ) )
                & FORMAT ( [Date], "mmmm" ),
        --Jul
        "Month Short",
            REPT ( _ZeroLenSpace, 12 - MONTH ( [Date] ) )
                & FORMAT ( [Date], "mmm" ),
        --31/07/2021
        "EOM", EOMONTH ( [Date], 0 ),
        // week =============================================
        --202128
        "Year Week",
            YEAR ( [Date] ) * 100
                + WEEKNUM ( [Date], 2 ),
        --28 (number)
        "Week", WEEKNUM ( [Date], 2 ),
        --W28
        "Week W", "W" & FORMAT ( WEEKNUM ( [Date], 2 ), "00" ),
        //week day ==================================================
        --1
        "Week Day", WEEKDAY ( [Date], 2 ),
        --Monday
        "Week Day Long",
            REPT ( _ZeroLenSpace, 12 - WEEKDAY ( [Date], 2 ) )
                & FORMAT ( [Date], "dddd" ),
        --Mon
        "Week Day Short",
            REPT ( _ZeroLenSpace, 12 - WEEKDAY ( [Date], 2 ) )
                & FORMAT ( [Date], "ddd" ),
        //other =====================================================
        --12 (number)
        "Day of Month", DAY ( [Date] ),
        --193
        "Day of Year", DATEDIFF ( DATE ( YEAR ( [Date] ), 1, 1 ), [Date], DAY ) + 1,
        --43151 (number)
        "Day Since 1900", VALUE ( [Date] ),
        //DATEDIFF ==================================================
        "Days From Now", DATEDIFF ( TODAY (), [Date], DAY ),
        "Weeks From Now", DATEDIFF ( TODAY (), [Date], WEEK ),
        "Month From Now", DATEDIFF ( TODAY (), [Date], MONTH ),
        "Quarters From Now", DATEDIFF ( TODAY (), [Date], QUARTER ),
        "Years From Now", DATEDIFF ( TODAY (), [Date], YEAR )
    )
RETURN
    _FullCalendar
