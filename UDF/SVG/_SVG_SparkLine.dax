(
    _DateColumn: ANYREF EXPR,
    _Value: ANYREF EXPR,
    _SVG_Width: INT64 VAL,
    _SVG_Height: INT64 VAL,
    _Color: STRING VAL,
    _ColorBottom: STRING VAL,
    _ColorTop: STRING VAL
) =>
VAR _StartDate =
    -- first date of the period
    MIN ( _DateColumn )
VAR _EndDate =
    -- last date of the period
    MAX ( _DateColumn )
VAR _YMinValue =
    -- min value during the period
    MINX (
        ALLSELECTED ( _DateColumn ),
        _Value
    )
VAR _YMaxValue =
    -- max value during the period
    MAXX (
        ALLSELECTED ( _DateColumn ),
        _Value
    )
VAR _SparklineTable =
    ADDCOLUMNS (
        VALUES ( _DateColumn ),
        "X",
            INT ( (_SVG_Width - 75 ) * DIVIDE ( _DateColumn - _StartDate, _EndDate - _StartDate ) ),
        "Y",
            INT (
                _SVG_Height
                    * DIVIDE ( _Value - _YMinValue, _YMaxValue - _YMinValue )
            )
    )
VAR _SparklineTableEndDate =
    FILTER ( _SparklineTable, _DateColumn = _EndDate )
VAR _Line =
    CONCATENATEX (
        _SparklineTable,
        [X] & "," & _SVG_Height - [Y],
        " ",
        _DateColumn
    ) // Add to SVG, and verify Data Category is set to Image URL for this measure
VAR _LastPoint =
    CONCATENATEX (
        _SparklineTableEndDate,
        "cx='" & [X] & "' cy='" & _SVG_Height - [Y] & "' ",
        " ",
        _DateColumn, ASC
    )
VAR _LastPointLabel =
    CONCATENATEX (
        _SparklineTableEndDate,
        "x='" & [X] + 5 & "' y='" & _SVG_Height / 2 & "' ",
        " ",
        _DateColumn, ASC
    )
VAR _LastPointY =
    SELECTCOLUMNS ( _SparklineTableEndDate, "Y", _SVG_Height - [Y] )
VAR _LineBack =
    CONCATENATEX (
        _SparklineTableEndDate,
        " 0" & "," & _SVG_Height - [Y],
        " ",
        _DateColumn, ASC
    )
VAR _LastDateValue =
    CALCULATE ( _Value, _SparklineTableEndDate )
VAR _SVG =
    "data:image/svg+xml;utf8," & //
    "<svg xmlns='http://www.w3.org/2000/svg' width='" & _SVG_Width & "' height='" & _SVG_Height & "'>" & //
    "<clipPath id='clipBottom' clipPathUnits='evenodd'>" & //
        "<rect x='0' y='0' width='" & _SVG_Width & "' height='" & _LastPointY & "' />" & //
    "</clipPath>" & "<clipPath id='clipTop' clipPathUnits='evenodd'>" & //
        "<rect x='0' y='" & _LastPointY & "' width='" & _SVG_Width & "' height='" & _SVG_Height - _LastPointY & "' />" & //
    "</clipPath>" & //
    "<polyline clip-path='url(#clipBottom)' fill='" & _ColorBottom & "' fill-opacity='0.4' stroke='" & _Color & "' stroke-width='0.2' points=' " & _Line & _LineBack & " '/>" & //
    "<polyline clip-path='url(#clipTop)' fill='" & _ColorTop & "' fill-opacity='0.4' stroke='" & _Color & "' stroke-width='0.5' points=' " & _Line & _LineBack & " '/>" & //
    "<polyline fill='" & _Color & "' fill-opacity='0' stroke='" & _Color & "' stroke-width='3' points=' " & _Line & " '/>" & //
    "<circle fill='" & _Color & "' fill-opacity='1' stroke='" & _Color & "' stroke-width='3' " & _LastPoint & " r='3' />" & //
    "<text alignment-baseline='middle' text-anchor='start' font-family='Segoe UI' font-weight='normal' font-size='36' fill='" & _Color & "' " & _LastPointLabel & " >"
        & FORMAT ( ROUND ( _LastDateValue, 2 ), "#,0%" ) & "</text>" & //
    "<text alignment-baseline='"
        & IF ( _LastPointY > ( _SVG_Height / 2 ), "top", "bottom" ) & "' text-anchor='end' font-family='Segoe UI' font-weight='normal' font-size='20' fill='#847974' " & " x='" & _SVG_Width & "' y='"
        & IF ( _LastPointY > ( _SVG_Height / 2 ), 20, _SVG_Height - 20 ) & "' >"
        & FORMAT ( ROUND ( _YMinValue, 2 ), "#,0%" ) & "-"
        & FORMAT ( ROUND ( _YMaxValue, 2 ), "#,0%" ) & "</text>" & //
    "</svg>"
RETURN
    _SVG
